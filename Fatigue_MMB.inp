!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! LICENSE: 
! Copyright (c) 2023, CraCS Research Group at Aalborg University, Siemens Gamesa RE, and Universitat de Girona
! All rights reserved.
!
! This project is licensed under the terms of the GNU Lesser General Public License v3.0 license.
!
! The author would like to kindly emphasize that the following rules are respected when using the code:
! 
! * Use of the code in source and binary forms, with or without modification are permitted provided that due reference is given to the author, the 
! code and publications describing the implemented models.
! * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MODEL DESCRIPTION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! This is an ANSYS Mechanical APDL input file to run a fatigue analysis of a Mixed Mode Bending (MMB) model with user-defined cohesive elements
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% PARAMETERS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Parameters
Displacement = 4 ! Applied displacement in each arm (mm)
st_time_substepqs = 0.05 ! Starting time substep for quasi-static loading step
max_time_substepqs = 0.05 ! Maximum time substep for quasi-static loading step
NumCycles = 10E4 ! Total number of cycles to be simulated
IncTargetCycles = 10 ! Target of cycles per substep to be simulated
Totala = 20 ! Total crack growth to be simulated
IncTargeta = 0.1 ! Target of crack growth per substep to be simulated
! In this case, the target given is in terms of crack growth and not of number of cycles
st_time_substepf = IncTargeta/Totala ! Starting time substep for quasi-static loading step
max_time_substepf =IncTargeta/Totala ! Maximum time substep for quasi-static loading step
!
! The following material properties are defined in "Trabal, G. G., Bak, B. L. V., Chen, B., Carreras, L., Lindgaard, E. (2022). 
! An adaptive floating node based formulation for the analysis of multiple delaminations under high cycle fatigue loading. 
! Composites Part A: Applied Science and Manufacturing, 107036. https://doi.org/10.1016/j.compositesa.2022.107036"
! Material properties of the specimen
E11 = 120E3 ! Young modulus 1 (MPa)
E22 = 10.5E3 ! Young modulus 2 (MPa)
E33 = 10.5E3 ! Young modulus 3 (MPa)
G12 = 5.3E3 ! Shear modulus 12 (MPa)
G13 = 5.3E3 ! Shear modulus 13 (MPa)
G23 = 3.5E3 ! Shear modulus 23 (MPa)
v12 = 0.3 ! Poisson's ratio 12
v13 = 0.3 ! Poisson's ratio 13
v23 = 0.51 ! Poisson's ratio 23
v21 = v12*E22/E11 ! Poisson's ratio 21
!
! Properties cohesive elements
! Integration parameters
NIntP = 2.0 ! Number of integration points                                     
IntMethodFlag = 1.0  ! Flag to select the integration method: Newton-Cotes (1) or Gauss (2)
! Quasi-static
tauI = 30 ! Mode I interface strength (MPa)                                        
tauII = 60 ! Mode II interface strength (MPa)                                        
KStiffness = 30e6 ! Penalty stiffness. Initial stiffness of undamaged cohesive element (MPa)                                  
BKeta = 2.73 ! Benzeggagh–Kenane criterion coefficient                                 
GIc = 0.260 ! Mode I critical energy release rate / fracture toughness (N/mm)                                    
GIIc = 1.002 ! Mode II critical energy release rate / fracture toughness (N/mm)  
! Fatigue
ParisCI = 3.08E-3 ! Paris-law parameter C in mode I (mm/cycle)
ParisCII = 1.49E-1 ! Paris-law parameter C in mode II (mm/cycle)
ParisCm = 458087 ! Paris-law parameter C for mixed mode (mm/cycle)
ParismI = 5.4 ! Paris-law parameter m in mode I
ParismII = 4.5 ! Paris-law parameter m in mode II
Parismm = 4.94 ! Paris-law parameter m for mixed mode
!
! Define the dimensions of the specimen
LengthFix = 75  ! Half span length of MMB fixture (mm)
Width = 0.75 ! Width (mm)
Thickness = 3.1 ! Thickness (mm)
Leverlength = 63.2 ! Lever laoding arm length (mm) for 50% mode mixity
InitialCrackLength = 45 ! Initial crack length (mm)
!
! Define the mesh sizes in each direction
ElemSizeX = 0.1 ! Element length in the x direction / along the specimen's length (mm)
ElemSizeY = 0.75 ! Element length in the y direction / along each arm's thickness (mm)
ElemSizeZ = 0.75 ! Element length in the z direction / along the specimen's width (mm)
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MATERIAL PROPERTIES %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open preprocessor
/prep7
! The composite material properties are defined
MP, EX, 1, E11
MP, EY, 1, E22
MP, EZ, 1, E33
MP, GXY, 1, G12
MP, GXZ, 1, G13
MP, GYZ, 1, G23
MP, PRXY, 1, v12
MP, PRXZ, 1, v13
MP, PRYZ, 1, v23
! Set units as mm and MPa
/UNITS, MPA
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% GEOMETRY & MESH LEVER %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! The geometry is created 
Halfthickness = Thickness/2
Quarterthickness = Thickness/4
Halfwidth = Width/2
!
! Define lines
K, 1, 0, Halfthickness*10, Halfwidth
K, 2, LengthFix, Halfthickness*10, Halfwidth
K, 3, LengthFix + Leverlength, Halfthickness*10, Halfwidth
L, 1, 2
L, 2, 3
!
! Define the very rigid material
MP, EX, 3, E11*10000
MP, EY, 3, E22*10000
MP, EZ, 3, E33*10000
MP, GXY, 3, G12*10000
MP, GXZ, 3, G13*10000
MP, GYZ, 3, G23*10000
MP, PRXY, 3, v12
MP, PRXZ, 3, v13
MP, PRYZ, 3, v23
!
! Define the element types
ET, 5, BEAM188
SECTYPE, , BEAM, RECT 
SECDATA, 1, 1
MAT, 3
! Mesh
LMESH, 1, 2
!
! %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% GEOMETRY & MESH SPECIMEN %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! The geometry is created 
! Define the keypoints
K, 4, 0, -Thickness/2, -Width/2
K, 5, InitialCrackLength, -Thickness/2, -Width/2
K, 6, LengthFix, -Thickness/2, -Width/2
K, 7, 2*LengthFix, -Thickness/2, -Width/2
K, 8, 2*LengthFix, 0, - Width/2
K, 9, LengthFix, 0, -Width/2
K, 10, InitialCrackLength, 0, -Width/2
K, 11, 0, 0, -Width/2
K, 12, 0, 0, - Width/2
K, 13, 0, Thickness/2, - Width/2
K, 14, InitialCrackLength, Thickness/2, - Width/2
K, 15, LengthFix, Thickness/2, -Width/2
K, 16, 2*LengthFix, Thickness/2, - Width/2
! Define the areas based on the keypoints
A, 4, 5, 10, 11
A, 5, 6, 9, 10
A, 6, 7, 8, 9
A, 12, 10, 14, 13
A, 10, 9, 15, 14
A, 9, 8, 16, 15
! The areas 1 to 6 are extruded in the z direction the width of the specimen
VEXT, 1, 6, 1, 0, 0, Width
!
! Define the element size
ALLSEL, ALL 
LSEL, S, LOC, X, InitialCrackLength/2
LSEL, R, LOC, Z, -Width/2
! LESIZE, ALL, ElemSizeX, , , 0.2
LESIZE, ALL, ElemSizeX
ALLSEL, ALL 
LSEL, S, LOC, X, InitialCrackLength/2
LSEL, R, LOC, Z, Width/2
! LESIZE, ALL, ElemSizeX, , , 5
LESIZE, ALL, ElemSizeX
ALLSEL, ALL 
LSEL, S, LOC, X, (LengthFix+InitialCrackLength)/2
! LESIZE, ALL, ElemSizeX/5
LESIZE, ALL, ElemSizeX
ALLSEL, ALL 
LSEL, S, LOC, X, 3*LengthFix/2
! LESIZE, ALL, ElemSizeX*5
LESIZE, ALL, ElemSizeX
ALLSEL, ALL 
LSEL, S, LOC, Z, 0
LESIZE, ALL, ElemSizeZ
ALLSEL, ALL 
LSEL, S, LOC, Y, Quarterthickness
LESIZE, ALL, ElemSizeY
ALLSEL, ALL 
LSEL, S, LOC, Y, -Quarterthickness
LESIZE, ALL, ElemSizeY
!
! Define the element types
ET, 1, SOLID185
KEYOPT, 1, 2, 2
! Define local coordinate system and associate it to the unmeshed volumes
LOCAL, 11, 0, 0, 0, 0, 0, 0
VATT, 1, , 1, 11
! Mesh the geometry
ALLSEL, ALL
VMESH, ALL
CSYS, 0
!
! Mesh cohesive elements
! Provide input for user-defined cohesive elements
ET, 3, USER300
TYPE, 3
! The user-element is defined through commands USRELEM and USRDOF
! USRELEM (number of nodes, number of dimensions (2 or 3), the element shape (ANYSHAPE due to 0 thickness), the number of real constants, the number of saved variables, the number of variables saved in results files 
! (not including stress and total strain data), key for element formulation control (0 to create one’s own material code within the element formulation, or 1 to use ANSYS standard material subroutine or USERMAT), 
! maximum number of integration points (if not using one's own material code), the key for the element stress state (if not using one's own material code) and flag for if the element stiffness matrices are symmetric
! (0) or not (1))
! USRDOF (command operation to specify, list or delete the DOFs; and then the list of DOFs (which should be some of UX, UY, UZ, ROTX, ROTY, ROTZ, AX, AY, AZ, ...) 
NREAL = 20 ! Number of real constants
NSAVEVARS = (NIntP**2)*20 ! Number of saved variables
NRSLTVAR = 0 ! Number of variables saved in results files (not including stress and total strain data)
USRELEM, 8, 3, BRICK, NREAL, NSAVEVARS, NRSLTVAR, 0, , , 1 
SHPP, OFF, ALL ! To allow for brick-shaped cohesive elements (dactivates element shape checking)
USRDOF, DEFINE, UX, UY, UZ
REAL, 2
R, 2, NIntP, IntMethodFlag, tauI, tauII, KStiffness, BKeta
RMORE, GIc, GIIc, visc, ParisCI, ParisCII, ParisCm
RMORE, ParismI, ParismII, Parismm, IncTargeta, sI, sII
! Indicate with 0 that no fatigue initiation should occur
RMORE, 0, InitiationLength
!
! Mesh first with ANSYS'own cohesive elements
ET, 4, INTER205
TYPE, 4
ALLSEL, ALL
ESEL, S, TYPE, , 1
CZMESH, , , 0, Y, 0
!
! And then modify the type
ALLSEL, ALL
ESEL, S, TYPE, , 4
EMODIF, ALL, TYPE, 3
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% BOUNDARY AND LOADING CONDITIONS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Restrict all displacements on the bottom edge
ALLSEL, ALL 
NSEL, S, LOC, X, 0
NSEL, R, LOC, Y, -Halfthickness
D, ALL, ALL, 0 
! Restrict displacements UY and UZ on the bottom roller
ALLSEL, ALL 
NSEL, S, LOC, X, LengthFix*2 - LengthFix/10000, LengthFix*2 + LengthFix/10000
NSEL, R, LOC, Y, -Halfthickness
D, ALL, UY, 0 
D, ALL, UZ, 0 
!
! Restrict Y displacement and rotations in the beam
ALLSEL, ALL 
NSEL, R, LOC, Y, Halfthickness*10
D, ALL, UZ, 0
D, ALL, ROTX, 0
D, ALL, ROTY, 0
!
! Boundary condition on the top edge modelled through constraint euqations on UX and UY
ALLSEL, ALL 
NSEL, S, LOC, X, 0
NSEL, R, LOC, Y, 10*Halfthickness
*GET, NodeEdge, NODE, 0, NUM, MIN 
ALLSEL, ALL 
NSEL, S, LOC, X, 0
NSEL, R, LOC, Y, Halfthickness
*GET, CURRN, NODE, 0, NUM, MIN ! Minimum number in the node set
*GET, NUM_NODES, NODE, 0, COUNT ! Number of nodes in the node set
*DO, I, 1, NUM_NODES
    CE, NEXT, 0, NodeEdge, UX, 1, CURRN, UX, -1
    CE, NEXT, 0, NodeEdge, UY, 1, CURRN, UY, -1
	*GET, CURRN, NODE, CURRN, NXTH ! Next higher node number
*ENDDO
! Boundary condition on the top roller modelled through constraint euqations on UY
ALLSEL, ALL 
NSEL, S, LOC, X, LengthFix 
NSEL, R, LOC, Y, 10*Halfthickness
*GET, NodeEdge, NODE, 0, NUM, MIN 
ALLSEL, ALL 
NSEL, S, LOC, X, LengthFix - LengthFix/10000, LengthFix + LengthFix/10000
NSEL, R, LOC, Y, Halfthickness
*GET, CURRN, NODE, 0, NUM, MIN ! Minimum number in the node set
*GET, NUM_NODES, NODE, 0, COUNT ! Number of nodes in the node set
*DO, I, 1, NUM_NODES
    CE, NEXT, 0, NodeEdge, UY, 1, CURRN, UY, -1
	*GET, CURRN, NODE, CURRN, NXTH ! Next higher node number
*ENDDO
!
! Apply plain-strain conditions
ALLSEL, ALL
D, ALL, UZ, 0
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% STATIC SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open solution processor
/SOLU
!
! Quasi-static loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepqs, max_time_substepqs/1E20, max_time_substepqs
! Large deflections are included
NLGEOM, ON
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 100
! Avoid iterations
AUTOTS, OFF
!
! Apply displacement at the edge of the lever
ALLSEL, ALL 
NSEL, S, LOC, X, LengthFix + Leverlength
NSEL, R, LOC, Y, Halfthickness*10
D, ALL, UY, -Displacement
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsStatic, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% FATIGUE SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Fatigue loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepf, max_time_substepf/1E6, max_time_substepf
! Large deflections are included
NLGEOM, ON
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 1000
!
! Apply displacement at the edge of the lever
ALLSEL, ALL 
NSEL, S, LOC, X, LengthFix + Leverlength
NSEL, R, LOC, Y, Halfthickness*10
D, ALL, UY, -Displacement
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsFatigue, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% SOLVING %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
FINISH