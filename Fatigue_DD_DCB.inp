!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! LICENSE: 
! Copyright (c) 2023, CraCS Research Group at Aalborg University, Siemens Gamesa RE, and Universitat de Girona
! All rights reserved.
!
! This project is licensed under the terms of the GNU Lesser General Public License v3.0 license.
!
! The author would like to kindly emphasize that the following rules are respected when using the code:
! 
! * Use of the code in source and binary forms, with or without modification are permitted provided that due reference is given to the author, the 
! code and publications describing the implemented models.
! * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MODEL DESCRIPTION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! This is an ANSYS Mechanical APDL input file to run a fatigue analysis of a double-delamination DCB model with user-defined cohesive elements
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% PARAMETERS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Parameters
Displacement = 4.2 ! Applied displacement in each arm (mm)
st_time_substepqs = 0.05 ! Starting time substep for quasi-static loading step
max_time_substepqs = 0.05 ! Maximum time substep for quasi-static loading step
NumCycles = 10E4 ! Total number of cycles to be simulated
IncTargetCycles = 10 ! Target of cycles per substep to be simulated
Totala = 10 ! Total crack growth to be simulated
IncTargeta1 = 0.1 ! Target of crack growth per substep to be simulated
IncTargeta2 = 0.1 ! Target of crack growth per substep to be simulated
! In this case, the target given is in terms of crack growth and not of number of cycles
st_time_substepf = IncTargeta1/Totala ! Starting time substep for quasi-static loading step
max_time_substepf =IncTargeta1/Totala ! Maximum time substep for quasi-static loading step
!
! The following material properties are defined in "Trabal, G. G., Bak, B. L. V., Chen, B., Carreras, L., Lindgaard, E. (2022). 
! An adaptive floating node based formulation for the analysis of multiple delaminations under high cycle fatigue loading. 
! Composites Part A: Applied Science and Manufacturing, 107036. https://doi.org/10.1016/j.compositesa.2022.107036"
! Material properties DCB specimen
E11 = 120E3 ! Young modulus 1 (MPa)
E22 = 10.5E3 ! Young modulus 2 (MPa)
E33 = 10.5E3 ! Young modulus 3 (MPa)
G12 = 5.3E3 ! Shear modulus 12 (MPa)
G13 = 5.3E3 ! Shear modulus 13 (MPa)
G23 = 3.5E3 ! Shear modulus 23 (MPa)
v12 = 0.3 ! Poisson's ratio 12
v13 = 0.3 ! Poisson's ratio 13
v23 = 0.51 ! Poisson's ratio 23
v21 = v12*E22/E11 ! Poisson's ratio 21
!
! Properties cohesive elements for bottom crack / crack 1
NIntP1 = 2.0 ! Number of integration points                                     
IntMethodFlag1 = 1.0  ! Flag to select the integration method: Newton-Cotes (1) or Gauss (2)
! Quasi-static
tauI1 = 30 ! Mode I interface strength (MPa)                                        
tauII1 = 60 ! Mode II interface strength (MPa)                                        
KStiffness1 = 30e6 ! Penalty stiffness. Initial stiffness of undamaged cohesive element (N/mm3)                                  
BKeta1 = 2.73 ! Benzeggagh–Kenane criterion coefficient                                 
GIc1 = 0.260 ! Mode I critical energy release rate / fracture toughness (N/mm)                                    
GIIc1 = 1.002 ! Mode II critical energy release rate / fracture toughness (N/mm) 
! Fatigue
ParisCI1 = 3.08E-3 ! Paris-law parameter C in mode I (mm/cycle)
ParisCII1 = 1.49E-1 ! Paris-law parameter C in mode II (mm/cycle)
ParisCm1 = 458087 ! Paris-law parameter C for mixed mode (mm/cycle)
ParismI1 = 5.4 ! Paris-law parameter m in mode I
ParismII1 = 4.5 ! Paris-law parameter m in mode II
Parismm1 = 4.94 ! Paris-law parameter m for mixed mode
!
! Properties cohesive elements for top crack / crack 2
NIntP2 = 2.0 ! Number of integration points                                     
IntMethodFlag2 = 1.0  ! Flag to select the integration method: Newton-Cotes (1) or Gauss (2)
! Quasi-static
tauI2 = 30 ! Mode I interface strength (MPa)                                        
tauII2 = 60 ! Mode II interface strength (MPa)                                        
KStiffness2 = 30e6 ! Penalty stiffness. Initial stiffness of undamaged cohesive element (N/mm3)                                  
BKeta2 = 2.73 ! Benzeggagh–Kenane criterion coefficient                                 
GIc2 = 0.260 ! Mode I critical energy release rate / fracture toughness (N/mm)                                    
GIIc2 = 1.002 ! Mode II critical energy release rate / fracture toughness (N/mm)    
! Fatigue
ParisCI2 = 3.08e-3 ! Paris-law parameter C in mode I (mm/cycle)
ParisCII2 = 1.49E-1 ! Paris-law parameter C in mode II (mm/cycle)
ParisCm2 = 458087 ! Paris-law parameter C for mixed mode (mm/cycle)
ParismI2 = 5.4 ! Paris-law parameter m in mode I
ParismII2 = 4.5 ! Paris-law parameter m in mode II
Parismm2 = 4.94 ! Paris-law parameter m for mixed mode
! 
! The following geometry details are also described in Krueger, 2014
Width = 0.1 ! Width (mm)
Length = 120 ! Total length, considering the fixed section (mm)
ThicknessTop = 1.06 ! Thickness of top arm (mm)
ThicknessMid = 1.06 ! Thickness of middle arm (mm)
ThicknessBot = 1.06 ! Thickness of bottom arm (mm)
InitialCrackLengthBot = 50 ! Initial bottom crack length (mm)
InitialCrackLengthTop = 40 ! Initial top crack length (mm)
!
! Define the mesh sizes in each direction
ElemSizeX = 0.1  ! Element length in the x direction / along the specimen's length (mm)
ElemSizeY = 0.35 ! Element length in the y direction / along each arm's thickness (mm)
ElemSizeZ = 0.1 ! Element length in the z direction / along the specimen's width (mm)
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MATERIAL PROPERTIES %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open preprocessor
/prep7
! The composite material properties are defined
MP, EX, 1, E11
MP, EY, 1, E22
MP, EZ, 1, E33
MP, GXY, 1, G12
MP, GXZ, 1, G13
MP, GYZ, 1, G23
MP, PRXY, 1, v12
MP, PRXZ, 1, v13
MP, PRYZ, 1, v23
! Set units as mm and MPa
/UNITS, MPA
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% GEOMETRY %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Define the keypoints
K, 1, 0, -ThicknessBot, -Width/2
K, 2, Length - InitialCrackLengthBot, -ThicknessBot, -Width/2
K, 3, Length - InitialCrackLengthTop, -ThicknessBot, -Width/2
K, 4, Length, -ThicknessBot, -Width/2
K, 5, Length, 0, -Width/2
K, 6, Length - InitialCrackLengthTop, 0, -Width/2
K, 7, Length - InitialCrackLengthBot, 0, -Width/2
K, 8, 0, 0, -Width/2
K, 9, Length - InitialCrackLengthTop, 0, -Width/2
K, 10, Length, 0, -Width/2
K, 11, 0, ThicknessMid, -Width/2
K, 12, Length - InitialCrackLengthBot, ThicknessMid, -Width/2
K, 13, Length - InitialCrackLengthTop, ThicknessMid, -Width/2
K, 14, Length, ThicknessMid, -Width/2
K, 15, Length, ThicknessMid, -Width/2
K, 16, Length, ThicknessMid + ThicknessTop, -Width/2
K, 17, Length - InitialCrackLengthTop, ThicknessMid + ThicknessTop, -Width/2
K, 18, Length - InitialCrackLengthBot, ThicknessMid + ThicknessTop, -Width/2
K, 19, 0, ThicknessMid + ThicknessTop, -Width/2
! Define the areas based on the keypoints
A, 1, 2, 7, 8
A, 2, 3, 6, 7
A, 3, 4, 5, 6
A, 8, 7, 12, 11
A, 7, 9, 13, 12
A, 9, 10, 14, 13
A, 11, 12, 18, 19
A, 12, 13, 17, 18
A, 13, 15, 16, 17
! The areas 1 to 4 are extruded in the z direction the width of the specimen
VEXT, 1, 9, 1, 0, 0, Width
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MESH SOLID %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Mesh composite solid elements
! Define element type
ET, 1, SOLID185
KEYOPT, 1, 2, 2 ! Enhanced strain formulation
! Define mesh size
! In the length direction
ALLSEL, ALL
LSEL, S, LOC, Y, -ThicknessBot -ThicknessBot/1000, -ThicknessBot + ThicknessBot/1000
LSEL, A, LOC, Y, 0 -ThicknessBot/1000, 0 + ThicknessBot/1000
LSEL, A, LOC, Y, ThicknessMid -ThicknessBot/1000, ThicknessMid + ThicknessBot/1000,
LSEL, A, LOC, Y, ThicknessMid + ThicknessTop -ThicknessBot/1000, ThicknessMid + ThicknessTop + ThicknessBot/1000,
LSEL, R, LOC, Z, -Width/2 -Width/1000, -Width/2 + Width/1000
LESIZE, ALL, ElemSizeX
ALLSEL, ALL
LSEL, S, LOC, Y, -ThicknessBot -ThicknessBot/1000, -ThicknessBot + ThicknessBot/1000
LSEL, A, LOC, Y, 0 -ThicknessBot/1000, 0 + ThicknessBot/1000
LSEL, A, LOC, Y, ThicknessMid -ThicknessBot/1000, ThicknessMid + ThicknessBot/1000,
LSEL, A, LOC, Y, ThicknessMid + ThicknessTop -ThicknessBot/1000, ThicknessMid + ThicknessTop + ThicknessBot/1000,
LSEL, R, LOC, Z, Width/2 -Width/1000, Width/2 + Width/1000 
LESIZE, ALL, ElemSizeX
! In the thickness direction
ALLSEL, ALL
LSEL, S, LOC, Y, -ThicknessBot/2 - ThicknessBot/100, -ThicknessBot/2 + ThicknessBot/100
LSEL, A, LOC, Y, ThicknessMid/2 - ThicknessMid/100, ThicknessMid/2 + ThicknessMid/100
LSEL, A, LOC, Y, ThicknessMid+ThicknessTop/2 - ThicknessMid/100, ThicknessMid+ThicknessTop/2 + ThicknessMid/100
LESIZE, ALL, ElemSizeY
! In the width direction
ALLSEL, ALL
LSEL, S, LOC, Z, 0 - Width/100, 0 + Width/100
LESIZE, ALL, ElemSizeZ, , , , 1
! Define local coordinate system and associate it to the unmeshed volumes
LOCAL, 11, 0, 0, 0, 0, 0, 0
VATT, 1, , 1, 11
! Mesh the geometry
ALLSEL, ALL
VMESH, ALL
CSYS, 0
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MESH COHESIVE %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Define the cohesive elements of crack 1
ET, 2, USER300
TYPE, 2
! The user-element is defined through commands USRELEM and USRDOF
! USRELEM (number of nodes, number of dimensions (2 or 3), the element shape (ANYSHAPE due to 0 thickness), the number of real constants, the number of saved variables, the number of variables saved in results files 
! (not including stress and total strain data), key for element formulation control (0 to create one’s own material code within the element formulation, or 1 to use ANSYS standard material subroutine or USERMAT), 
! maximum number of integration points (if not using one's own material code), the key for the element stress state (if not using one's own material code) and flag for if the element stiffness matrices are symmetric
! (0) or not (1))
! USRDOF (command operation to specify, list or delete the DOFs; and then the list of DOFs (which should be some of UX, UY, UZ, ROTX, ROTY, ROTZ, AX, AY, AZ, ...) 
NREAL = 20 ! Number of real constants
NSAVEVARS = (NIntP1**2)*20 ! Number of saved variables
NRSLTVAR = 0 ! Number of variables saved in results files (not including stress and total strain data)
USRELEM, 8, 3, BRICK, NREAL, NSAVEVARS, NRSLTVAR, 0, , , 1 
SHPP, OFF, ALL ! To allow for brick-shaped cohesive elements (dactivates element shape checking)
USRDOF, DEFINE, UX, UY, UZ
REAL, 2
R, 2, NIntP1, IntMethodFlag1, tauI1, tauII1, KStiffness1, BKeta1
RMORE, GIc1, GIIc1, visc1, ParisCI1, ParisCII1, ParisCm1
RMORE, ParismI1, ParismII1, Parismm1, IncTargeta1, sI, sII 
! Indicate with 0 that no fatigue initiation should occur
RMORE, 0, InitiationLength
!
! Mesh first with ANSYS'own cohesive elements
ALLSEL, ALL
TB, CZM, 4, , , EXPO
TBDATA, 1, , , 
ESEL, S, TYPE, , 1
ET, 4, INTER205
TYPE, 4
MAT, 4
CZMESH, , , 0, Y, 0, ThicknessMid/10000
!
! And then modify the type
ALLSEL, ALL
ESEL, S, TYPE, , 4
EMODIF, ALL, TYPE, 2
! 
! Define crack 2
ET, 3, USER300
TYPE, 3
NREAL = 20
NSAVEVARS = (NIntP2**2)*20 
NRSLTVAR = 0 
USRELEM, 8, 3, BRICK, NREAL, NSAVEVARS, NRSLTVAR, 0, , , 1 
SHPP, OFF, ALL 
USRDOF, DEFINE, UX, UY, UZ
REAL, 3
R, 3, NIntP2, IntMethodFlag2, tauI2, tauII2, KStiffness2, BKeta2
RMORE, GIc2, GIIc2, visc2, ParisCI2, ParisCII2, ParisCm2
RMORE, ParismI2, ParismII2, Parismm2, IncTargeta2, sI, sII 
! Indicate with 0 that no fatigue initiation should occur
RMORE, 0, InitiationLength
ALLSEL, ALL
TB, CZM, 5, , , EXPO
TBDATA, 1, , , 
ESEL, S, TYPE, , 1
ET, 5, INTER205
TYPE, 5
MAT, 5
CZMESH, , , 0, Y, ThicknessMid, ThicknessMid/10000
ALLSEL, ALL
ESEL, S, TYPE, , 5
EMODIF, ALL, TYPE, 3
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% BOUNDARY AND LOADING CONDITIONS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Restrict UX and UY in the clamped side
! Top arm
ALLSEL, ALL
NSEL, S, LOC, X, 0 - Length/10000, 0 + Length/10000
NSEL, R, LOC, Y, ThicknessMid + ThicknessTop - ThicknessBot/10000, ThicknessMid + ThicknessTop + ThicknessBot/10000
D, ALL, UY, 0
D, ALL, UX, 0
! Bottom arm
ALLSEL, ALL
NSEL, S, LOC, X, 0 - Length/10000, 0 + Length/10000
NSEL, R, LOC, Y, -ThicknessBot - ThicknessBot/10000, -ThicknessBot + ThicknessBot/10000
D, ALL, UY, 0
D, ALL, UX, 0
!
! Apply plain-strain conditions 
ALLSEL, ALL
D, ALL, UZ, 0
!
! Large deflections are included
NLGEOM, ON
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% STATIC SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open solution processor
/SOLU
!
! Quasi-static loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepqs, max_time_substepqs/1E20, max_time_substepqs
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 100
!!
! Apply the displacement in the top and bottom arms
! Top arm
ALLSEL, ALL
NSEL, S, LOC, X, Length - Length/10000, Length + Length/10000
NSEL, R, LOC, Y, ThicknessMid+ThicknessTop - ThicknessMid/10000, ThicknessMid+ThicknessTop + ThicknessMid/10000
D, ALL, UY, Displacement
! Bottom arm
ALLSEL, ALL
NSEL, S, LOC, X, Length - Length/10000, Length + Length/10000
NSEL, R, LOC, Y, -ThicknessBot - ThicknessBot/10000, -ThicknessBot + ThicknessBot/10000
D, ALL, UY, -Displacement
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsStatic, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% FATIGUE SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Fatigue loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepf, max_time_substepf/1E6, max_time_substepf
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 1000
! Do not allow for bisections to occur:
CUTCONTROL, NOITERPREDICT, 1
!!
! Apply the displacement in the top and bottom arms
! Top arm
ALLSEL, ALL
NSEL, S, LOC, X, Length - Length/10000, Length + Length/10000
NSEL, R, LOC, Y, ThicknessMid+ThicknessTop - ThicknessMid/10000, ThicknessMid+ThicknessTop + ThicknessMid/10000
D, ALL, UY, Displacement
! Bottom arm
ALLSEL, ALL
NSEL, S, LOC, X, Length - Length/10000, Length + Length/10000
NSEL, R, LOC, Y, -ThicknessBot - ThicknessBot/10000, -ThicknessBot + ThicknessBot/10000
D, ALL, UY, -Displacement
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsFatigue, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% SOLVING %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
FINISH