!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! LICENSE: 
! Copyright (c) 2023, CraCS Research Group at Aalborg University, Siemens Gamesa RE, and Universitat de Girona
! All rights reserved.
!
! This project is licensed under the terms of the GNU Lesser General Public License v3.0 license.
!
! The author would like to kindly emphasize that the following rules are respected when using the code:
! 
! * Use of the code in source and binary forms, with or without modification are permitted provided that due reference is given to the author, the 
! code and publications describing the implemented models.
! * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MODEL DESCRIPTION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! This is an ANSYS Mechanical APDL input file to run a fatigue analysis of a Three-Point Bending test at 90 degrees with user-defined cohesive elements
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% PARAMETERS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Parameters
st_time_substepqs = 0.05 ! 0.025 ! Starting time substep for quasi-static loading step
max_time_substepqs = 0.05 ! 0.025 ! Maximum time substep for quasi-static loading step
NumCycles = 10E4 ! Total number of cycles to be simulated
IncTargetCycles = 10 ! Target of cycles per substep to be simulated
Totala = 10.0! Total crack growth to be simulated
IncTargeta = 0.05 ! Target of crack growth per substep to be simulated
! In this case, the target given is in terms of crack growth and not of number of cycles
st_time_substepf = IncTargeta/Totala ! Starting time substep for quasi-static loading step
max_time_substepf =IncTargeta/Totala ! Maximum time substep for quasi-static loading step
!
! The following material properties are defined in "Tao, C., Mukhopadhyay, S., Zhang, B., Kawashita, L. F., Qiu, J., Hallett, S. R. (2018). 
! An improved delamination fatigue cohesive interface model for complex three-dimensional multi-interface cases. 
! Composites Part A: Applied Science and Manufacturing, 107, 633–646. https://doi.org/10.1016/j.compositesa.2018.02.008"
! Except for:
! - The mode mixity Paris law parameters, which are from "Trabal, G. G., Bak, B. L. V., Chen, B., Carreras, L., Lindgaard, E. (2022). 
! An adaptive floating node based formulation for the analysis of multiple delaminations under high cycle fatigue loading.
! - The penalty stiffness, as determined in a convergence analysis for the DNS case.
! - The mode I interface strength, which is modified to better match the experimental static failure stress to the prediction by the 
! finite element model in a quasi-static simulation. This adjustment is done to guarantee that the application of the experimental 
! loads in the numerical analysis corresponds to a similar level of severity, which is defined as the ratio between the applied shear 
! stress and the static failure shear stress. The strength is increased from 60 MPa, in Tao et al., to 92 MPa to raise the numerically 
! predicted failure stress from 104.6 MPa to 127.9 MPa. The experimental static failure stress was 127.5 MPa.
! Material properties of the specimen
E11 = 161E3 ! Young modulus 1 (MPa)
E22 = 11.4E3 ! Young modulus 2 (MPa)
E33 = 11.4E3 ! Young modulus 3 (MPa)
G12 = 5.17E3 ! Shear modulus 12 (MPa)
G13 = 5.17E3 ! Shear modulus 13 (MPa)
G23 = 3.98E3 ! Shear modulus 23 (MPa)
v12 = 0.325 ! Poisson's ratio 12
v13 = 0.325 ! Poisson's ratio 13
v23 = 0.436 ! Poisson's ratio 23
v21 = v12*E22/E11 ! Poisson's ratio 21
E22 = 161E3 ! Young modulus 1 (MPa)
E11 = 11.4E3 ! Young modulus 2 (MPa)
E33 = 11.4E3 ! Young modulus 3 (MPa)
G12 = 5.17E3 ! Shear modulus 12 (MPa)
G23 = 5.17E3 ! Shear modulus 13 (MPa)
G13 = 3.98E3 ! Shear modulus 23 (MPa)
v21 = 0.325 ! Poisson's ratio 12
v23 = 0.325 ! Poisson's ratio 13
v13 = 0.436 ! Poisson's ratio 23
v12 = v21*E11/E22 ! Poisson's ratio 21
!
! Properties cohesive elements
! Integration parameters
NIntP = 2.0 ! Number of integration points                                     
IntMethodFlag = 1.0  ! Flag to select the integration method: Newton-Cotes (1) or Gauss (2)
! Quasi-static
tauI = 60 !92 ! 90 ! 60 ! Mode I interface strength (MPa)                                        
tauII = 90 ! Mode II interface strength (MPa)                                        
KStiffness = 1e6 ! Penalty stiffness. Initial stiffness of undamaged cohesive element (MPa)                                  
BKeta = 2.1 ! Benzeggagh–Kenane criterion coefficient                                 
GIc = 0.2 ! Mode I critical energy release rate / fracture toughness (N/mm)                                    
GIIc = 1.0 ! Mode II critical energy release rate / fracture toughness (N/mm)  
! Fatigue propagation
ParisCI = 6.51E-3 ! Paris-law parameter C in mode I (mm/cycle)
ParisCII = 8.70E-2 ! Paris-law parameter C in mode II (mm/cycle)
ParisCm = 22904 ! Paris-law parameter C for mixed mode (mm/cycle)
ParismI = 5.29 ! Paris-law parameter m in mode I
ParismII = 6.71 ! Paris-law parameter m in mode II
Parismm = 4.94 ! Paris-law parameter m for mixed mode
! Fatigue initiation
sI = 0.072 ! (MPa/decade)
sII = 0.071 ! (MPa/decade)
InitiationLength = 1.3 !2.3 ! 2.0 !2.3 ! Length of the crack introduced when fatigue initiation is detected (mm)
! Viscosity                                      
visc = 0 ! Viscous stabilization parameter for the damage in the element 
!
! The following geometry details are from "Kevin O’brien, T., Chawan, A. D., Krueger, R. ; Paris, I. L. (2002). 
! Transverse tension fatigue life characterization through flexure testing of composite materials. 
! In International Journal of Fatigue(Vol. 24). www.elsevier.com/locate/ijfatigue"
Width = 0.05 ! Width (mm) 
SupportSpan = 50 ! Support span (mm)
Length = 57.25 ! Specimen's length (mm)
Thickness = 4.93 ! Thickness (mm) 5.52 mm if using the value of the ply thickness
!
! Rollers geometry
DiamSuppRoller = 10 ! Diamater of the support rollers (mm)
DiamLoadRoller = 10 ! Diamater of the loading roller (mm)
!
! Define the mesh sizes in each direction
ElemSizeX = 0.5 ! Element length in the x direction / along the specimen's length (mm)
ElemSizeY = 0.05 ! Element length in the y direction / along each arm's thickness (mm)
ElemSizeZ = 0.05 ! Element length in the z direction / along the specimen's width (mm)
!
! Applied load
Severity = 1.1 !0.57 ! Ratio over the static failure load 
RealWidth = 6.35 ! Real width
StaticStrength = 127.5 ! Static strength (MPa)
StaticFailureLoad = 2*(RealWidth*(Thickness**2)*StaticStrength)/(3*SupportSpan) ! Static failure load (N)
Load = Severity*StaticFailureLoad*Width/RealWidth ! Applied load (N) 
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MATERIAL PROPERTIES %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open preprocessor
/prep7
! The composite material properties are defined
MP, EX, 1, E11
MP, EY, 1, E22
MP, EZ, 1, E33
MP, GXY, 1, G12
MP, GXZ, 1, G13
MP, GYZ, 1, G23
MP, PRXY, 1, v12
MP, PRXZ, 1, v13
MP, PRYZ, 1, v23
! The very rigid material properties are defined
MP, EX, 4, E11*1E6
MP, EY, 4, E22*1E6
MP, EZ, 4, E33*1E6
MP, GXY, 4, G12*1E6
MP, GXZ, 4, G13*1E6
MP, GYZ, 4, G23*1E6
MP, PRXY, 4, v12
MP, PRXZ, 4, v13
MP, PRYZ, 4, v23
! Set units as mm and MPa
/UNITS, MPA
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% GEOMETRY %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Specimen
! Define the keypoints
K, 1, 0, -Thickness/2, 0
K, 2, Length/2-SupportSpan/2, -Thickness/2, 0
K, 3, Length/2, -Thickness/2, 0
K, 4, Length/2+SupportSpan/2, -Thickness/2, 0
K, 5, Length, -Thickness/2, 0
K, 6, Length, 0, 0
K, 7, Length/2+SupportSpan/2, 0, 0
K, 8, Length/2, 0, 0
K, 9, Length/2-SupportSpan/2, 0, 0
K, 10, 0, 0, 0
K, 11, 0, Thickness/2, 0
K, 12, Length/2-SupportSpan/2, Thickness/2, 0
K, 13, Length/2, Thickness/2, 0
K, 14, Length/2+SupportSpan/2, Thickness/2, 0
K, 15, Length, Thickness/2, 0
! Define the areas based on the keypoints
A, 1, 2, 9, 10
A, 2, 3, 8, 9
A, 3, 4, 7, 8
A, 4, 5, 6, 7
A, 10, 9, 12, 11
A, 9, 8, 13, 12
A, 8, 7, 14, 13
A, 7, 6, 15, 14
!
! Rollers
! Introducing some small deviations in the positions to make contact work
CYL5, Length/2-SupportSpan/2, -Thickness/2+9.339E-4, Length/2-SupportSpan/2, -Thickness/2-DiamSuppRoller+9.339E-4
CYL5, Length/2+SupportSpan/2, -Thickness/2+9.339E-4, Length/2+SupportSpan/2, -Thickness/2-DiamSuppRoller+9.339E-4
CYL5, Length/2, Thickness/2-1.144E-3, Length/2, Thickness/2+DiamLoadRoller-1.144E-3
!
! All areas are extruded in the z direction the width of the specimen
VEXT, 1, 11, 1, 0, 0, Width
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MESH %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Mesh composite solid elements
! Define element type
MAT, 1
ET, 1, SOLID185
KEYOPT, 1, 2, 2 ! Enhanced strain formulation
! Define mesh size
! In the length direction
ALLSEL, ALL
LSEL, S, LOC, Y, -Thickness/2 - Thickness/100, -Thickness/2 + Thickness/100
LSEL, A, LOC, Y, 0 - Thickness/100, 0 + Thickness/100
LSEL, A, LOC, Y, Thickness/2 - Thickness/100, Thickness/2 + Thickness/100
LESIZE, ALL, ElemSizeX
ALLSEL, ALL
LSEL, S, LOC, Y, -Thickness/4 - Thickness/100, -Thickness/4 + Thickness/100
LSEL, A, LOC, Y, Thickness/4 - Thickness/100, Thickness/4 + Thickness/100
LESIZE, ALL, ElemSizeY
ALLSEL, ALL
LSEL, S, LOC, Z, 0 - Width/100, 0 + Width/100
LESIZE, ALL, ElemSizeZ
! Define local coordinate system and associate it to the unmeshed volumes
! LOCAL, 11, 0, 0, 0, 0, 90, 0
LOCAL, 11, 0, 0, 0, 0, 0, 0
VATT, 1, , 1, 11
! Mesh the specimen
ALLSEL, ALL
VMESH, 1, 8
! 
! Mesh the rollers
VMESH, 9, 11
! And assign to them material 4
ALLSEL, ALL
VSEL, S, VOLU, , 9, 11
ESLV, S
EMODIF, ALL, MAT, 4
ET, 4, SOLID185
KEYOPT, 4, 2, 2 ! Enhanced strain formulation
EMODIF, ALL, TYPE, 4
!
! Mesh cohesive elements
ET, 2, USER300
TYPE, 2
! The user-element is defined through commands USRELEM and USRDOF
! USRELEM (number of nodes, number of dimensions (2 or 3), the element shape (ANYSHAPE due to 0 thickness), the number of real constants, the number of saved variables, the number of variables saved in results files 
! (not including stress and total strain data), key for element formulation control (0 to create one’s own material code within the element formulation, or 1 to use ANSYS standard material subroutine or USERMAT), 
! maximum number of integration points (if not using one's own material code), the key for the element stress state (if not using one's own material code) and flag for if the element stiffness matrices are symmetric
! (0) or not (1))
! USRDOF (command operation to specify, list or delete the DOFs; and then the list of DOFs (which should be some of UX, UY, UZ, ROTX, ROTY, ROTZ, AX, AY, AZ, ...) 
NREAL = 20 ! Number of real constants
NSAVEVARS = (NIntP**2)*20 ! Number of saved variables
NRSLTVAR = 0 ! Number of variables saved in results files (not including stress and total strain data)
USRELEM, 8, 3, BRICK, NREAL, NSAVEVARS, NRSLTVAR, 0, , , 1 
SHPP, OFF, ALL ! To allow for brick-shaped cohesive elements (dactivates element shape checking)
USRDOF, DEFINE, UX, UY, UZ
REAL, 2
R, 2, NIntP, IntMethodFlag, tauI, tauII, KStiffness, BKeta
RMORE, GIc, GIIc, visc, ParisCI, ParisCII, ParisCm
RMORE, ParismI, ParismII, Parismm, IncTargeta, sI, sII  
! Indicate with 1 in real constant 19 that fatigue initiation should occur
RMORE, 1, InitiationLength 
! Mesh first with ANSYS'own cohesive elements
ALLSEL, ALL
TB, CZM, 3, , , EXPO
TBDATA, 1, , , 
ESEL, S, TYPE, , 1
ET, 3, INTER205
TYPE, 3
MAT, 3
CZMESH, , , 0, X, Length/2
! And then modify the type
ALLSEL, ALL
ESEL, S, TYPE, , 3
EMODIF, ALL, TYPE, 2
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% CONTACT %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Define the contact behaviour using pair-based contact
! For the loading roller
! Define the real constant set
REAL, 5
! First, define the contact elements
ET, 5, CONTA173
KEYOPT, 5, 5, 1
! Define frictionless contact
MP, MU, 5, 0
MAT, 5
! Create the contact elements
ALLSEL
ASEL, S, AREA, , 35, 38, 3
LSLA, S
NSLL, S
KSEL, S, KP, , 13
KSEL, A, KP, , 40
NSLK, A
TYPE, 5
ESURF
! First, define the contact elements
ET, 6, TARGE170
! Create the surface elements
ALLSEL
ASEL, S, AREA, , 55, 56
LSLA, S
NSLL, S
KSEL, S, KP, , 27
KSEL, A, KP, , 54
NSLK, A
TYPE, 6
ESURF
!
! For the left supporting roller
REAL, 7
! First, define the contact elements
ET, 7, CONTA173
KEYOPT, 7, 5, 1
! Define frictionless contact
MP, MU, 7, 0
MAT, 7
! Create the contact elements
ALLSEL
ASEL, S, AREA, , 13, 18, 5
LSLA, S
NSLL, S
KSEL, S, KP, , 2
KSEL, A, KP, , 29
NSLK, A
TYPE, 7
ESURF
! First, define the contact elements
ET, 8, TARGE170
! Create the surface elements
ALLSEL
ASEL, S, AREA, , 43, 44
LSLA, S
NSLL, S
KSEL, S, KP, , 17
KSEL, A, KP, , 44
NSLK, A
TYPE, 8
ESURF
!
! For the right supporting roller
REAL, 9
! First, define the contact elements
ET, 9, CONTA173
KEYOPT, 9, 5, 1
! Define frictionless contact
MP, MU, 9, 0
MAT, 9
! Create the contact elements
ALLSEL
ASEL, S, AREA, , 22, 26, 4
LSLA, S
NSLL, S
KSEL, S, KP, , 4
KSEL, A, KP, , 34
NSLK, A
TYPE, 9
ESURF
! First, define the contact elements
ET, 10, TARGE170
! Create the surface elements
ALLSEL
ASEL, S, AREA, , 48, 49
LSLA, S
NSLL, S
KSEL, S, KP, , 21
KSEL, A, KP, , 48
NSLK, A
TYPE, 10
ESURF
!
ALLSEL
CNCHECK, MORPH
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% BOUNDARY AND LOADING CONDITIONS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Restrict movement of the support rollers
ALLSEL, ALL
VSEL, S, VOLU, , 9, 10
ESLV, S
NSLE, S
D, ALL, ALL
!
! Restrict UX in the top roller
ALLSEL, ALL
VSEL, S, VOLU, , 11
ESLV, S
NSLE, S
D, ALL, UX, 0
!
! Restrict UZ everywhere
ALLSEL, ALL
D, ALL, UZ, 0
!
! Restrict UX
ALLSEL, ALL
NSEL, S, LOC, X, Length/2 - Length/10000, Length/2 + Length/10000
NSEL, R, LOC, Y, Thickness/2 - Thickness/10000, Thickness/2 + Thickness/10000
D, ALL, UX, 0
!
! Large deflections are included
NLGEOM, ON
! 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% STATIC SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open solution processor
/SOLU
!
! Quasi-static loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepqs, max_time_substepqs/1E3, max_time_substepqs
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 10
!!
! Apply the load on the top loading roller
ALLSEL, ALL
! Using the nodes on the top surfaces
ASEL, S, AREA, , 53, 54
LSLA, S
NSLL, S
*GET, NUM_NODES, NODE, 0, COUNT ! Number of nodes in the node set
F, ALL, FY, -Load/NUM_NODES
D, ALL, UX, 0
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsStatic, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% FATIGUE SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Fatigue loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepf, max_time_substepf/1E6, max_time_substepf
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 1000
! Do not allow for bisections to occur:
CUTCONTROL, NOITERPREDICT, 1
!!
! Apply the load on the top loading roller
ALLSEL, ALL
! Using the nodes on the top surfaces
ASEL, S, AREA, , 53, 54
LSLA, S
NSLL, S
*GET, NUM_NODES, NODE, 0, COUNT ! Number of nodes in the node set
F, ALL, FY, -Load/NUM_NODES
D, ALL, UX, 0
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsFatigue, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% SOLVING %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
FINISH
