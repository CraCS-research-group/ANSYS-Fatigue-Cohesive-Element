!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! LICENSE: 
! Copyright (c) 2023, Siemens Gamesa RE
! All rights reserved.
!
! The author would like to kindly emphasize that the following rules are respected when using the code. 
!   * Use of the code in source and binary forms, with or without modification are permitted provided that due reference is given to the author, 
!   the code and publications describing the implemented models.
!   * The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
!
! DISCLAIMER:
! THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
! IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE
! LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
! SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
! CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
! POSSIBILITY OF SUCH DAMAGE.
!
! Please send your comments or questions to Inigo Urcelay Oca: iuoc@mp.aau.dk / inigo.oca@siemensgamesa.com 
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MODEL DESCRIPTION %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! This is an ANSYS Mechanical APDL input file to run a fatigue analysis of Double Cantilever Beam (DCB) model with user-defined cohesive elements
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% PARAMETERS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Parameters
st_time_substepqs = 0.05 ! Starting time substep for quasi-static loading step
max_time_substepqs = 0.05 ! Maximum time substep for quasi-static loading step
NumCycles = 10E4 ! Total number of cycles to be simulated
IncTargetCycles = 10 ! Target of cycles per substep to be simulated
Totala = 1.0 ! 5 ! Total crack growth to be simulated
IncTargeta = 0.05 ! Target of crack growth per substep to be simulated
! In this case, the target given is in terms of crack growth and not of number of cycles
st_time_substepf = IncTargeta/Totala ! Starting time substep for quasi-static loading step
max_time_substepf =IncTargeta/Totala ! Maximum time substep for quasi-static loading step
!
! The following material properties are defined in "Tao, C., Mukhopadhyay, S., Zhang, B., Kawashita, L. F., Qiu, J., Hallett, S. R. (2018). 
! An improved delamination fatigue cohesive interface model for complex three-dimensional multi-interface cases. 
! Composites Part A: Applied Science and Manufacturing, 107, 633–646. https://doi.org/10.1016/j.compositesa.2018.02.008".
! Except for:
! - The mode mixity Paris law parameters, which are from "Trabal, G. G., Bak, B. L. V., Chen, B., Carreras, L., Lindgaard, E. (2022). 
! An adaptive floating node based formulation for the analysis of multiple delaminations under high cycle fatigue loading. 
! Composites Part A: Applied Science and Manufacturing, 107036. https://doi.org/10.1016/j.compositesa.2022.107036"
! - The initiation mode II S-N slope, which is from "May, M., Hallett, S. R. (2011). 
! An advanced model for initiation and propagation of damage under fatigue loading - part I: Model formulation. 
! Composite Structures, 93(9), 2340–2349. https://doi.org/10.1016/j.compstruct.2011.03.022"
! - The penalty stiffness, which was obtained from a convergence analysis.
! Material properties of the specimen
E11 = 161E3 ! Young modulus 1 (MPa)
E22 = 11.4E3 ! Young modulus 2 (MPa)
E33 = 11.4E3 ! Young modulus 3 (MPa)
G12 = 5.17E3 ! Shear modulus 12 (MPa)
G13 = 5.17E3 ! Shear modulus 13 (MPa)
G23 = 3.98E3 ! Shear modulus 23 (MPa)
v12 = 0.325 ! Poisson's ratio 12
v13 = 0.325 ! Poisson's ratio 13
v23 = 0.436 ! Poisson's ratio 23
v21 = v12*E22/E11 ! Poisson's ratio 21
!
! Properties cohesive elements
! Integration parameters
NIntP = 2.0 ! Number of integration points                                     
IntMethodFlag = 1.0  ! Flag to select the integration method: Newton-Cotes (1) or Gauss (2)
! Quasi-static
tauI = 60 ! Mode I interface strength (MPa)                                        
tauII = 90 ! Mode II interface strength (MPa)                                        
KStiffness = 1e6 ! Penalty stiffness. Initial stiffness of undamaged cohesive element (MPa)                                  
BKeta = 2.73 ! Benzeggagh–Kenane criterion coefficient                                 
GIc = 0.2 ! Mode I critical energy release rate / fracture toughness (N/mm)                                    
GIIc = 1.0 ! Mode II critical energy release rate / fracture toughness (N/mm)  
! Fatigue propagation
ParisCI = 6.51E-3 ! Paris-law parameter C in mode I (mm/cycle)
ParisCII = 8.70E-2 ! Paris-law parameter C in mode II (mm/cycle)
ParisCm = 458087 ! Paris-law parameter C for mixed mode (mm/cycle)
ParismI = 5.29 ! Paris-law parameter m in mode I
ParismII = 6.71 ! Paris-law parameter m in mode II
Parismm = 4.94 ! Paris-law parameter m for mixed mode
! Fatigue initiation
sI = 0.072 ! (MPa/decade)
sII = 0.12 ! (MPa/decade)
InitiationLength = 2.3 ! Length of the crack introduced when fatigue initiation is detected (mm)
!
! The following geometry details are from "May, M., Hallett, S. R. (2011). 
! An advanced model for initiation and propagation of damage under fatigue loading - part I: Model formulation. 
! Composite Structures, 93(9), 2340–2349. https://doi.org/10.1016/j.compstruct.2011.03.022"
Width = 0.05 ! Width (mm)
TotalLength = 10.125 ! Total length (mm)
OverlapLength = 6.375 ! Overlap length (mm)
Thickness = 3.01 ! Total thickness (mm)
!
! Define the mesh sizes in each direction
ElemSizeX = 0.05 ! Element length in the x direction / along the specimen's length (mm)
ElemSizeY = 0.05 ! Element length in the y direction / along each arm's thickness (mm)
ElemSizeZ = 0.05 ! Element length in the z direction / along the specimen's width (mm)
!
! Applied load
Severity = 0.7 ! Ratio over the static failure load
StaticStrength = 82.5
Load = Severity*StaticStrength*Width*OverlapLength ! Applied load (N)
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MATERIAL PROPERTIES %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open preprocessor
/prep7
! The composite material properties are defined
MP, EX, 1, E11
MP, EY, 1, E22
MP, EZ, 1, E33
MP, GXY, 1, G12
MP, GXZ, 1, G13
MP, GYZ, 1, G23
MP, PRXY, 1, v12
MP, PRXZ, 1, v13
MP, PRYZ, 1, v23
! Set units as mm and MPa
/UNITS, MPA
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% GEOMETRY %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Define the keypoints
K, 1, 0, -Thickness/2, -Width/2
K, 2, TotalLength/2 - OverlapLength/2, -Thickness/2, - Width/2
K, 3, TotalLength/2 - OverlapLength/2, 0, - Width/2
K, 4, 0, 0, - Width/2
K, 5, TotalLength/2 + OverlapLength/2, -Thickness/2, - Width/2
K, 6, TotalLength/2 + OverlapLength/2, 0, - Width/2
K, 7, TotalLength/2 + OverlapLength/2, Thickness/2, - Width/2
K, 8, TotalLength/2 - OverlapLength/2, Thickness/2, - Width/2
K, 9, TotalLength, 0, - Width/2
K, 10, TotalLength, Thickness/2, - Width/2
! Define the areas based on the keypoints
A, 1, 2, 3, 4
A, 2, 5, 6, 3
A, 3, 6, 7, 8
A, 6, 9, 10, 7
! The areas 1 to 2 are extruded in the z direction the width of the specimen
VEXT, 1, 4, 1, 0, 0, Width
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% MESH %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Mesh composite solid elements
! Define element type
ET, 1, SOLID185
KEYOPT, 1, 2, 2 ! Enhanced strain formulation
! Define mesh size
! In the length direction
ALLSEL, ALL
LSEL, S, LOC, Y, -Thickness/2 - Thickness/100, -Thickness/2 + Thickness/100
LSEL, A, LOC, Y, 0 - Thickness/100, 0 + Thickness/100
LSEL, A, LOC, Y, Thickness/2 - Thickness/100, Thickness/2 + Thickness/100
LESIZE, ALL, ElemSizeX
ALLSEL, ALL
LSEL, S, LOC, Y, -Thickness/4 - Thickness/100, -Thickness/4 + Thickness/100
LSEL, A, LOC, Y, Thickness/4 - Thickness/100, Thickness/4 + Thickness/100
LESIZE, ALL, ElemSizeY
ALLSEL, ALL
LSEL, S, LOC, Z, 0 - Width/100, 0 + Width/100
LESIZE, ALL, ElemSizeZ
! Define local coordinate system and associate it to the unmeshed volumes
LOCAL, 11, 0, 0, 0, 0, 0, 0
VATT, 1, , 1, 11
! Mesh the geometry
ALLSEL, ALL
VMESH, ALL
CSYS, 0
!
! Mesh cohesive elements
ET, 2, USER300
TYPE, 2
! The user-element is defined through commands USRELEM and USRDOF
! USRELEM (number of nodes, number of dimensions (2 or 3), the element shape (ANYSHAPE due to 0 thickness), the number of real constants, the number of saved variables, the number of variables saved in results files 
! (not including stress and total strain data), key for element formulation control (0 to create one’s own material code within the element formulation, or 1 to use ANSYS standard material subroutine or USERMAT), 
! maximum number of integration points (if not using one's own material code), the key for the element stress state (if not using one's own material code) and flag for if the element stiffness matrices are symmetric
! (0) or not (1))
! USRDOF (command operation to specify, list or delete the DOFs; and then the list of DOFs (which should be some of UX, UY, UZ, ROTX, ROTY, ROTZ, AX, AY, AZ, ...) 
NREAL = 20 ! Number of real constants
NSAVEVARS = (NIntP**2)*20 ! Number of saved variables
NRSLTVAR = 0 ! Number of variables saved in results files (not including stress and total strain data)
USRELEM, 8, 3, BRICK, NREAL, NSAVEVARS, NRSLTVAR, 0, , , 1 
SHPP, OFF, ALL ! To allow for brick-shaped cohesive elements (deactivates element shape checking)
USRDOF, DEFINE, UX, UY, UZ
REAL, 2
R, 2, NIntP, IntMethodFlag, tauI, tauII, KStiffness, BKeta
RMORE, GIc, GIIc, visc, ParisCI, ParisCII, ParisCm
RMORE, ParismI, ParismII, Parismm, IncTargeta, sI, sII
! Indicate with 1 in real constant 19 that fatigue initiation should occur
RMORE, 1, InitiationLength 
! Mesh first with ANSYS'own cohesive elements
ET, 3, INTER205
CZMESH, , , 0, Y, 0
! And then modify the type
ALLSEL, ALL
ESEL, S, TYPE, , 3
EMODIF, ALL, TYPE, 2
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% BOUNDARY AND LOADING CONDITIONS %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Select nodes in the top and bottom faces and restrict UY
ALLSEL, ALL
NSEL, S, LOC, Y, -Thickness/2 - Thickness/100, -Thickness/2 + Thickness/100
NSEL, A, LOC, Y, Thickness/2 - Thickness/100, Thickness/2 + Thickness/100
D, ALL, UY, 0
!
! Select nodes in the left face and restrict UX and UY
ALLSEL, ALL
NSEL, S, LOC, X, 0 - TotalLength/1000, 0 + TotalLength/1000
D, ALL, UX, 0
D, ALL, UY, 0
!
! Restrict UZ everywhere 
ALLSEL, ALL
D, ALL, UZ, 0
!
! Large deflections are included
NLGEOM, ON
! 
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% STATIC SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Open solution processor
/SOLU
!
! Quasi-static loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepqs, max_time_substepqs/1E20, max_time_substepqs
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 100
!!
! Apply the load in the right face
ALLSEL, ALL
NSEL, S, LOC, X, TotalLength - TotalLength/10000, TotalLength + TotalLength/10000
*GET, NUM_NODES, NODE, 0, COUNT ! Number of nodes in the node set
F, ALL, FX, -Load/NUM_NODES
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsStatic, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% FATIGUE SUBSTEP %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
! Fatigue loading substep
ANTYPE, STATIC
! Defining the starting and maximum time substep
DELTIM, st_time_substepf, max_time_substepf/1E6, max_time_substepf
! The maximum number of errors is set to 99999990
/NERR, , 99999990
! The maximum number of equilibrium iterations is increased to 100
NEQIT, 550
! Do not allow for bisections to occur:
CUTCONTROL, NOITERPREDICT, 1
!!
! Apply the load in the right face
ALLSEL, ALL
NSEL, S, LOC, X, TotalLength - TotalLength/10000, TotalLength + TotalLength/10000
*GET, NUM_NODES, NODE, 0, COUNT ! Number of nodes in the node set
F, ALL, FX, -Load/NUM_NODES
!
! Ask for results in all increments
OUTRES, ALL, 1
!
! Everything is selected once again
ALLSEL
!
! The load step is written and solved
SOLVE
!
*GET, NumSubstepsFatigue, ACTIVE, 0, SOLU, NCMSS
!
!%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%% SOLVING %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
!
FINISH